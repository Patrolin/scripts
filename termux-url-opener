#!/usr/bin/fish

set true 1
set false 0

while true
	read -P 'Download video? [Yn]: ' video
	switch $video
		case '' y Y
			set video $true
		case n N
			set video $false
		case '*'
			continue
	end
	read -P 'Max height? [480]: ' height
	switch $height
		case ''
			set height 480
		case best BEST
			set height 9999
		case '*'
			continue
	end
	set -l path
	set -l playlist (youtube-dl --playlist-items '1' --get-filename -o '%(playlist)s' $argv)
	switch $playlist
		case '' NA
			set -l date (date +%Y-%m-%d -d (youtube-dl --playlist-end '1' --get-filename -o '%(upload_date)s' $argv))
			set path "%(title)s $date.%(ext)s"
		case '*'
			read -P "Playlist name? [$playlist]: " user_playlist
			if test -z $user_playlist
				set user_playlist $playlist
			end
			set path "$user_playlist/%(playlist_index)s %(title)s.%(ext)s"
	end
	set -l options -i --no-mtime --sleep-interval '1'
	if test $video = $true
		set -a options -f "bestvideo[height<=$height]+bestaudio/best[height<=$height]/best"
		set -a options --merge-output-format 'mkv' --recode-video 'mkv' # debian is stupid and has decades old ffmpeg and doesn't let you recode into mp4
		set -a options --write-sub --write-auto-sub --sub-lang 'en,cs,rechat' --embed-subs
		#set -a options --write-thumbnail --embed-thumbnail # debian's youtube-dl is terrible and has broken dependencies for mp4 and doesn't support mkv
		set -a options -o "~/storage/downloads/video/$path"
	else
		set -a options -f "best[height<=$height]/best"
		set -a options -x --audio-format 'mp3' # youtube-dl is dumb and doesn't let you recode audio
		set -a options --add-metadata --metadata-from-title '%(artist)s - %(title)s'
		set -a options -o "~/storage/downloads/audio/$path"
	end
	youtube-dl $options $argv
	and exit
	printf 'Failed...\n\n'
end
